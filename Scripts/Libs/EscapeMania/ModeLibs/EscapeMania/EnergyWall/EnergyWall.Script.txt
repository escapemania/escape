#Include "Libs/EscapeMania/ModeLibs/EscapeMania/Rooms/RoomsRequestRespawn.Script.txt" as RoomsRequestRespawn

#Const C_LoopTriggerTag "LoopTrigger"
#Const C_MediaTrackerClipPrefix "Wall "
#Const C_DeathTriggerTag "EnergyWallDeathTrigger"

declare Integer G_WallState;

Void Init() {
	G_WallState = 10;
	foreach (Player in Players) {
		declare UI = UIManager.GetUI(Player);
		UI.UISequence = CUIConfig::EUISequence::CustomMTClip;
		UI.UISequence_CustomMTClip = C_MediaTrackerClipPrefix ^ G_WallState;
	}
}

Void CompletePuzzle() {
	G_WallState = 0;
	foreach (Player in Players) {
		declare UI = UIManager.GetUI(Player);
		UI.UISequence_CustomMTClip = C_MediaTrackerClipPrefix ^ G_WallState;
		UI.QueueMessage(1000, 1, CUIConfig::EMessageDisplay::Big, "");
	}
}

Void OnRespawn(CSmPlayer Player) {
	if (G_WallState > 0) {
		declare UI = UIManager.GetUI(Player);
		UI.UISequence_CustomMTClip = C_MediaTrackerClipPrefix ^ G_WallState;
	}
}

Void LowerWall() {
	if (G_WallState > 0) {
		G_WallState -= 1;

		if (G_WallState == 1) {
			CompletePuzzle();
			return;
		}

		if (G_WallState > 0) {
			foreach (Player in Players) {
				declare UI = UIManager.GetUI(Player);
				UI.UISequence_CustomMTClip = C_MediaTrackerClipPrefix ^ G_WallState;
				UI.QueueMessage(1000, 1, CUIConfig::EMessageDisplay::Big, "");
			}
		}
	}
}

Void Loop() {
	foreach (Event in PendingEvents) {
		switch (Event.Type) {
			case CSmModeEvent::EType::OnPlayerTriggersWaypoint: {
				if (Event.Landmark.Tag == C_LoopTriggerTag) {
					LowerWall();
				} else if (Event.Landmark.Tag == C_DeathTriggerTag && G_WallState > 1) {
					RoomsRequestRespawn::RequestRespawn(Event.Player);
				}
			}
		}
	}
}
