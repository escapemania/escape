#Include "Libs/EscapeMania/ModeLibs/EscapeMania/Rooms/RoomsRequestRespawn.Script.txt" as RoomsRequestRespawn
#Include "Libs/EscapeMania/ModeLibs/EscapeMania/MediaTracker/MediaTracker.Script.txt" as MediaTracker

#Const C_EntranceLandmarkTag "Challenge_Start"
#Const C_SpawnLandmarkTag "Challenge_Spawn"
#Const C_EndLandmarkTag "Challenge_End"
#Const C_MediaTrackerObserver "Challenge Cam"
#Const C_MediaTrackerPlayer "Challenge Blindfold"
#Const C_MediaTrackerPriority 5

declare CSmPlayer G_CurrentPlayer;
declare Boolean G_IsComplete;
declare CMapLandmark G_SpawnLocation;

Void ResetMediaTrackers() {
	foreach (Player in Players) {
		MediaTracker::UnsetMediaTracker(Player, C_MediaTrackerPriority);
	}
}

Void ResetPuzzle() {
	G_IsComplete = False;
	ResetMediaTrackers();
}

Void CompletePuzzle() {
	G_IsComplete = True;
	foreach (Player in Players) {
		declare UI = UIManager.GetUI(Player);
		UI.QueueMessage(1000, 1, CUIConfig::EMessageDisplay::Big, "");
	}
	ResetMediaTrackers();
	G_CurrentPlayer = Null;
}

Void OnEnd(CSmPlayer Player) {
	ResetMediaTrackers();
	CompletePuzzle();
}

Void OnLeave(CSmPlayer Player) {
	G_CurrentPlayer = Null;
	ResetMediaTrackers();
}

Void OnEntrance(CSmPlayer Player) {
	if (G_IsComplete) {
		return;
	}
	if (G_CurrentPlayer == Player) {
		OnLeave(Player);
		return;
	}

	if (G_CurrentPlayer != Null) {
		RoomsRequestRespawn::RequestRespawn(Player);
		declare UI = UIManager.GetUI(Player);
		UI.QueueMessage(3000, 1, CUIConfig::EMessageDisplay::Big, " ");
		return;
	}

	G_CurrentPlayer = Player;
	RoomsRequestRespawn::RequestRespawnAtLocation(Player, G_SpawnLocation);
	MediaTracker::SetMediaTracker(Player, C_MediaTrackerPlayer, C_MediaTrackerPriority);
	foreach (OtherPlayer in Players) {
		if (OtherPlayer != Player) {
			RoomsRequestRespawn::RequestRespawn(OtherPlayer);
			MediaTracker::SetMediaTracker(OtherPlayer, C_MediaTrackerObserver, C_MediaTrackerPriority);
		}
	}
}

Void OnRespawn(CSmPlayer Player, CMapLandmark Location) {
	if (Player == G_CurrentPlayer && Location != G_SpawnLocation) {
		OnLeave(Player);
	}
}

Void Init() {
	G_CurrentPlayer = Null;
	G_IsComplete = False;
	
	foreach (Landmark in MapLandmarks) {
		if (Landmark.Tag == C_SpawnLandmarkTag) {
			G_SpawnLocation = Landmark;
		}
	}
}

Void Loop() {
	foreach (Event in PendingEvents) {
		switch (Event.Type) {
			case CSmModeEvent::EType::OnPlayerTriggersWaypoint: {
				if (Event.Landmark.Tag == C_EntranceLandmarkTag) {
					OnEntrance(Event.Player);
				} else if (Event.Landmark.Tag == C_EndLandmarkTag) {
					OnEnd(Event.Player);
				}
			}
		}
	}
}