#Include "Libs/Zrx/ModeLibs/Common/VirtualPickups/VirtualPickups.Script.txt" as VirtualPickups
#Include "Libs/EscapeMania/ModeLibs/EscapeMania/Items/Items.Script.txt" as Items

#Include "TextLib" as TL

#Const C_SuccessLandmarkTagPrefix "Wordle_Success_"
#Const C_SuccessLandmarkTag_1 "Wordle_Success_1"
#Const C_SuccessLandmarkTag_2 "Wordle_Success_2"
#Const C_SuccessLandmarkTag_3 "Wordle_Success_3"
#Const C_SuccessLandmarkTag_4 "Wordle_Success_4"
#Const C_ItemLandmarkTag "Wordle_Item"


declare Integer[CSmPlayer] G_IsSuccess;
declare Boolean G_IsComplete;

Boolean IsComplete() {
	return G_IsComplete;
}

Void ResetPuzzle() {
	foreach (Player => IsSuccess in G_IsSuccess) {
		G_IsSuccess[Player] = 0;
	}
	G_IsComplete = False;

	// Destroy Key Items
	foreach (Pickup in VirtualPickups::GetAll()) {
		if (Items::IsKeyItem(Pickup)) {
			VirtualPickups::Destroy(Pickup.Id);
		}
	}
}

Void CompletePuzzle() {
	G_IsComplete = True;
	
	// Destroy Key Items
	foreach (Pickup in VirtualPickups::GetAll()) {
		if (Items::IsKeyItem(Pickup)) {
			VirtualPickups::Destroy(Pickup.Id);
		}
	}
	
	// Create Key Items
	foreach (Landmark in MapLandmarks) {
		if (Landmark.Tag == C_ItemLandmarkTag) {
			Items::CreateKeyItem(Landmark);
		}
	}
	
	// Tell players something is happening
	foreach (Player in Players) {
		declare UI = UIManager.GetUI(Player);
		UI.QueueMessage(1000, 1, CUIConfig::EMessageDisplay::Big, "");
	}
}

Void OnEnterTest(CSmPlayer Player, Text Tag) {
	if (G_IsComplete) {
		return;
	}
	
	if (Tag == C_SuccessLandmarkTag_1) {
		G_IsSuccess[Player] = 1;
	} else if (Tag == C_SuccessLandmarkTag_2) {
		G_IsSuccess[Player] = 2;
	} else if (Tag == C_SuccessLandmarkTag_3) {
		G_IsSuccess[Player] = 3;
	} else if (Tag == C_SuccessLandmarkTag_4) {
		G_IsSuccess[Player] = 4;
	}

	declare Boolean isSuccess1 = False;
	declare Boolean isSuccess2 = False;
	declare Boolean isSuccess3 = False;
	declare Boolean isSuccess4 = False;
	foreach (Player => Success in G_IsSuccess) {
		if (Success == 1) {
			isSuccess1 = True;
		} else if (Success == 2) {
			isSuccess2 = True;
		} else if (Success == 3) {
			isSuccess3 = True;
		} else if (Success == 4) {
			isSuccess4 = True;
		}
	}
	
	if (isSuccess1 && isSuccess2 && isSuccess3 && isSuccess4) {
		CompletePuzzle();
	} else {
		declare UI = UIManager.GetUI(Player);
		UI.QueueMessage(1000, 1, CUIConfig::EMessageDisplay::Big, "");
	}
}

Void OnLeaveTest(CSmPlayer Player) {
	G_IsSuccess[Player] = 0;
}

Void OnRespawn(CSmPlayer Player) {
	OnLeaveTest(Player);
}

Void Init() {
	G_IsComplete = False;
}

Void Loop() {
	foreach (Event in PendingEvents) {
		switch (Event.Type) {
			case CSmModeEvent::EType::OnPlayerTriggersWaypoint: {
				if (TL::StartsWith(C_SuccessLandmarkTagPrefix, Event.Landmark.Tag)) {
					OnEnterTest(Event.Player, Event.Landmark.Tag);
				} else if (Event.Landmark.Tag == C_ItemLandmarkTag) {
					// Pickup
					declare VirtualPickups::K_VirtualPickup[] LandmarkPickups = VirtualPickups::GetByHolder(Event.Landmark);
					if (LandmarkPickups.count > 0) {
						declare VirtualPickups::K_VirtualPickup Pickup = LandmarkPickups[0];
						if (Items::IsKeyItem(Pickup)) {
							VirtualPickups::SetHolder(Pickup.Id, Event.Player);
						}
					}
				} else {
					OnLeaveTest(Event.Player);
				}
			}
		}
	}
}
